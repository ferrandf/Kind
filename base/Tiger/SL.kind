// straight-line programs Type and (very inefficient) interpreter
// statement
type Tiger.SL.Stm {
  CompoundStm(fst: Tiger.SL.Stm, snd: Tiger.SL.Stm)
  AssignStm(id: String, exp: Tiger.SL.Exp)
  PrintStm(exp_list: List<Tiger.SL.Exp>)
}

// expression
type Tiger.SL.Exp {
  IdExp(id: String)
  NumExp(num: Int) // #TODO include other numbers?
  OpExp(op: Tiger.SL.Binop, fst: Tiger.SL.Exp, snd: Tiger.SL.Exp)
  EseqExp(stm: Tiger.SL.Stm, exp: Tiger.SL.Exp)
}

type Tiger.SL.ExpList {
  PairExpList(head: Tiger.SL.Exp, tail: Tiger.SL.ExpList)
  LastExpList(head: Tiger.SL.Exp)
}

type Tiger.SL.Binop {
  plus
  minus
  times
  div
}

maxargs(stm: Tiger.SL.Stm): Nat
  case stm {
    CompoundStm: Nat.max(maxargs(stm.fst), maxargs(stm.snd))
    AssignStm: 0
    PrintStm: List.length!(stm.exp_list)
  }

Tiger.SL.interpStm(stm: Tiger.SL.Stm, table: List<Pair<String, Int>>): List<Pair<String, Int>>
  case stm {
    CompoundStm:
      let new_table = Tiger.SL.interpStm(stm.fst, table)
      Tiger.SL.interpStm(stm.snd, new_table)
    AssignStm:
      let {i, new_table} = Tiger.SL.interpExp(stm.exp, table)
      {stm.id, i} & new_table
    PrintStm:
      for exp in stm.exp_list with table:
        let {i, table} = Tiger.SL.interpExp(exp, table)
        log(Int.show(i))
        table
      table
  }

Tiger.SL.lookup(id: String, table: List<Pair<String, Int>>): Int
  case table {
    nil:
      0 :: Int
    cons:
      let {table_id, val} = table.head
      if id =? table_id then
        val
      else
        Tiger.SL.lookup(id, table.tail)
  }

Tiger.SL.interpExp(exp: Tiger.SL.Exp, table: List<Pair<String, Int>>): Pair<Int, List<Pair<String, Int>>>
  case exp {
    IdExp:
      {Tiger.SL.lookup(exp.id, table), table}
    NumExp:
      {exp.num, table}
    OpExp:
      let {fst_val, table} = Tiger.SL.interpExp(exp.fst, table)
      let {snd_val, table} = Tiger.SL.interpExp(exp.snd, table)
      case exp.op {
        plus:
          {fst_val + snd_val, table}
        minus:
          {fst_val - snd_val, table}
        times:
          {fst_val * snd_val, table}
        div:
          {fst_val / snd_val, table}
      }
    EseqExp:
      let table = Tiger.SL.interpStm(exp.stm, table)
      Tiger.SL.interpExp(exp.exp, table)
  }

